plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'idea'
	id 'java'
	id "com.github.sherter.google-java-format" version "0.8"
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '1'
sourceCompatibility = JavaVersion.VERSION_12
targetCompatibility = JavaVersion.VERSION_12

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	compileOnly 'org.jetbrains:annotations:17.0.0'
	testCompileOnly 'org.jetbrains:annotations:17.0.0'

	implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.1.1'
	implementation 'org.seasar.doma:doma:2.24.0'
	annotationProcessor 'org.seasar.doma:doma:2.24.0'

	runtimeOnly 'org.postgresql:postgresql'

	testImplementation ('org.springframework.boot:spring-boot-starter-test') {
		exclude module: 'junit'
	}
	testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'

	testImplementation 'com.tngtech.archunit:archunit:0.10.2'
	testImplementation 'com.tngtech.archunit:archunit-junit5-api:0.10.2'
	testRuntimeOnly 'com.tngtech.archunit:archunit-junit5-engine:0.10.2'

	testCompile "org.mockito:mockito-core:2.27.0"
}

idea.module.inheritOutputDirs = true

test {
	useJUnitPlatform()
}

task copyDomaResources(type: Sync)  {
	from sourceSets.main.resources.srcDirs
	into compileJava.destinationDir
	include 'doma.compile.config'
	include 'META-INF/**/*.sql'
	include 'META-INF/**/*.script'
}

compileJava {
	dependsOn copyDomaResources
	options.encoding = 'UTF-8'
	options.compilerArgs = ['-Adoma.domain.converters=com.example.dao.DomainConvertersProvider']
}

compileTestJava {
	options.encoding = 'UTF-8'
	options.compilerArgs = ['-proc:none']
}

task runApp(type: JavaExec, group: 'Application') {
	main = 'com.example.App'
	classpath(configurations.runtimeClasspath, sourceSets.main.output)
}
